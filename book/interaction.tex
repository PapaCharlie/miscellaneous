\chapter{Interacting with the server}
\section{HTTP input}
Liquidsoap can create a source that pulls its data from an HTTP location. This location can 
be a distant file or playlist, or an icecast or shoutcast stream.

To use it in your script, simply create a source that way:

\begin{verbatim}
# url is a HTTP location, like
# http://radiopi.org:8080/reggae
source = input.http(url)
\end{verbatim}
This operator will pull regulary the given location for its data, so it should be used for 
locations that are assumed to be available most of the time. If not, it might generate unnecessary 
traffic and polute the logs. In this case, it is perhaps better to inverse the paradigm and 
use the \href{harbor.html}{input.harbor} operator.

\section{Harbor input}
Liquidsoap is also able to receive a source using icecast or shoutcast source protocol with 
the \verb+input.harbor+ operator. Using this operator, the running liquidsoap will open 
a network socket and wait for an incoming connection.

This operator is very useful to seamlessly add live streams
into your final streams:
you configure the live source client to connect directly to liquidsoap,
and manage the switch to and from the live inside your script.

Additionally, liquidsoap can handle many simulataneous harbor sources on different ports, 
with finer-grained authentication schemes that can be particularly useful when used with
source clients designed for the shoutcast servers.

\subsection{Parameters}
The global parameters for harbor can be retreived using
\verb+liquidsoap --conf-descr-key harbor+. They are:

\begin{itemize}
\item \verb+harbor.bind_addr+: IP address on which the HTTP stream receiver should listen. The default is \verb+"0.0.0.0"+. You can use this parameter to restrict connections only to your LAN.
\item \verb+harbor.timeout+: Timeout for source connection, in seconds. Defaults to \verb+30.+.
\item \verb+harbor.verbose+: Print password used by source clients in logs, for debugging purposes. Defaults to: \verb+false+
\item \verb+harbor.reverse_dns+: Perform reverse DNS lookup to get the client's hostname from its IP. Defaults to: \verb+true+
\item \verb+harbor.icy_formats+: Content-type (mime) of formats which allow shout (ICY) metadata update. Defaults to:
\begin{verbatim}
["audio/mpeg"; "audio/aacp"; "audio/aac"; "audio/x-aac"; "audio/wav"; "audio/wave"]
\end{verbatim}


\end{itemize}
You also have per-source parameters. You can retreive them using the command 
\verb+liquidsoap -h input.harbor+. The most important one are:

\begin{itemize}
\item \verb+user+, \verb+password+: set a permanent login and password for this harbor source.
\item \verb+auth+: Authenticate the user according to a specific function.
\item \verb+port+: Use a custom port for this input.
\item \verb+icy+: Enable ICY (shoutcast) source connections.
\item \verb+id+: The mountpoint registered for the source is also the id of the source.

\end{itemize}
When using different ports with different harbor inputs, mountpoints are attributed
per-port. Hence, there can be a harbor input with mountpoint \verb+"foo"+ on port \verb+1356+
and a harbor input with mountpoint \verb+"foo"+ on port \verb+3567+. Additionaly, if an harbor 
source uses custom port \verb+n+ with shoutcast (ICY) source protocol enabled, shoutcast
source clients should set their connection port to \verb+n+1+.

The \verb+auth+ function is a function, that takes a pair \verb+(user,password)+ and returns a boolean representing whether the user 
should be granted access or not. Typical example can be:

\begin{verbatim}
def auth(user,password) = 
  # Call an external process to check 
  # the credentials:
  # The script will return the string 
  # "true" of "false"
  #
  # First call the script
  ret = get_process_lines("/path/to/script \
         --user=#{user} --password=#{password}")
  # Then get the first line of its output
  ret = list.hd(ret)
  # Finally returns the boolean represented 
  # by the output (bool_of_string can also 
  # be used)
  if ret == "true" then
    true
  else
    false
  end
end
\end{verbatim}
In the case of the \verb+ICY+ (shoutcast) source protocol, there is no \verb+user+ parameter 
for the source connection. Thus, the user used will be the \verb+user+ parameter passed 
to the \verb+input.harbor+ source.

When using a custom authentication function, in case of a \verb+ICY+ (shoutcast) connection, 
the function will receive this value for the username.

\subsection{Usage}
When using harbor inputs, you first set the required settings, as described above. Then, you define each source using \verb+input.harbor("mountpoint")+. This source is faillible and will become available when a source client is connected. 

The unlabeled parameter is the mount point that the source client may connect
to. It should be \verb+"/"+ for shoutcast source clients.

The source client may use any of the recognized audio input codec. Hence, when using shoucast source clients, you need to have compiled liquidsoap with mp3 decoding support (\verb+ocaml-mad+)

A sample code can be:

\begin{verbatim}
set("harbor.bind_addr","0.0.0.0")

# Some code...

# This defines a source waiting on mount point 
# /test-harbor
live = input.harbor("test-harbor",port=8080,password="xxx")

# This is the final stream.
# Uses the live source as soon as available,
# and don't wait for an end of track, since 
# we don't want to cut the beginning of the live
# stream.
#
# You may insert a jingle transition here...
radio = fallback(track_sensitive=false,
                 [live,files])
\end{verbatim}

\section{Harbor as HTTP server}
The harbor server can be used as a HTTP server. You 
can use the function \verb+harbor.http.register+ to register
HTTP handlers for GET and POST requests. Its parameters
are are follow:

\begin{verbatim}
harbor.http.register(port=8080,uri,handler)
\end{verbatim}
 where:

\begin{itemize}
\item \verb+port+ is the port where to receive incoming connections
\item \verb+uri+ is used to match requested uri. Perl regular expressions are accepted.
\item \verb+handler+ is the function used to process requests.

\end{itemize}
\verb+handler+ function has type:

\begin{verbatim}
(~method:string, ~protocol:string, ~data:string, 
 ~headers:[(string*string)], string)->string))->unit
\end{verbatim}
where:

\begin{itemize}
\item \verb+method+ is the HTTP method used by the client. Currently, one of \verb+"POST"+ or \verb+"GET"+
\item \verb+protocol+ is the HTTP protocol used by the client. Currently, one of \verb+"HTTP/1.0"+ or \verb+"HTTP/1.1"+
\item \verb+data+ is the data passed during a POST request
\item \verb+headers+ is the list of HTTP headers sent by the client
\item \verb+string+ is the (unparsed) uri requested by the client, e.g.: \verb+"/foo?var=bar"+

\end{itemize}
The \verb+handler+ function returns HTTP and HTML data to be sent to the client,
for instance:

\begin{verbatim}
HTTP/1.1 200 OK\r\n\
Content-type: text/html\r\n\
Content-Length: 35\r\n\
\r\n\
<html><body>It works!</body></html>
\end{verbatim}
(\verb+\r\n+ should always be used for line return
in HTTP content)

For convenience, a \verb+http_response+ function is provided to 
create a HTTP response string. It has the following type:

\begin{verbatim}
(?protocol:string,?code:int,?headers:[(string*string)],
 ?data:string)->string
\end{verbatim}
where:

\begin{itemize}
\item \verb+protocol+ is the HTTP protocol of the response (default \verb+HTTP/1.1+)
\item \verb+code+ is the response code (default \verb+200+)
\item \verb+headers+ is the response headers. It defaults to \verb+[]+ but an appropriate \verb+"Content-Length"+ header is added if not set by the user and \verb+data+ is not empty.
\item \verb+data+ is an optional response data (default \verb+""+)

\end{itemize}
Thess functions can be used to create your own HTTP interface. Some examples
are:

\subsection{Redirect Icecast's pages}
Some source clients using the harbor may also request pages that
are served by an icecast server, for instance listeners statistics.
In this case, you can register the following handler:

\begin{verbatim}
# Redirect all files other than /admin.* to icecast, located at localhost:8000
def redirect_icecast(~method,~protocol,~data,~headers,uri) =
   http_response(
     protocol=protocol,
     code=301,
     headers=[("Location","http://localhost:8000#{uri}")]
   )
end

# Register this handler at port 8005
# (provided harbor sources are also served
#  from this port).
harbor.http.register(port=8005,"^/(?!admin)",
                     redirect_icecast)
\end{verbatim}
Another alternative, less recommanded, is to
directly fetch the page's content from the Icecast server:

\begin{verbatim}
# Serve all files other
# than /admin.* by fetching data
# from Icecast, located at localhost:8000
def proxy_icecast(~method,~protocol,~data,~headers,uri) =
  def f(x) =
    # Replace Host
    if string.capitalize(fst(x)) == "HOST" then
      "Host: localhost:8000"
    else
      "#{fst(x)}: #{snd(x)}"
    end
  end
  headers = list.map(f,headers)
  headers = string.concat(separator="\r\n",headers)
  request =
    "#{method} #{uri} #{protocol}\r\n\
     #{headers}\r\n\r\n"
  get_process_output("echo #{quote(request)} | \
                      nc localhost 8000")
end

# Register this handler at port 8005
# (provided harbor sources are also served
#  from this port).
harbor.http.register(port=8005,"^/(?!admin)",
                     proxy_icecast)
\end{verbatim}
This method is not recommenced because some servers may not
close the socket after serving a request, causing \verb+nc+ and
liquidsoap to hang.

\subsection{Get metadata}
You can use harbor to register HTTP services to 
fecth/set the metadata of a source. For instance, 
using the \href{json.html}{JSON export function} \verb+json_of+:

\begin{verbatim}
meta = ref []

# s = some source

# Update current metadata
# converted in UTF8
def update_meta(m) =
  m = metadata.export(m)
  recode = string.recode(out_enc="UTF-8")
  def f(x) =
    (recode(fst(x)),recode(snd(x)))
  end
  meta := list.map(f,m)
end

# Apply update_metadata
# every time we see a new
# metadata
s = on_metadata(update_meta,s)

# Return the json content
# of meta
def get_meta(~method,~protocol,~data,~headers,uri) =
  m = !meta
  http_response(
    protocol=protocol,
    code=200,
    headers=[("Content-Type","application/json; charset=utf-8")],
    data=json_of(m)
  )
end

# Register get_meta at port 700
harbor.http.register(port=7000,"/getmeta",get_meta)
\end{verbatim}
Once the script is running, 
a GET/POST request for \verb+/getmeta+ at port \verb+7000+
returns the following:

\begin{verbatim}
HTTP/1.1 200 OK
Content-Type: application/json; charset=utf-8

{
  "genre": "Soul",
  "album": "The Complete Stax-Volt Singles: 1959-1968 (Disc 8)",
  "artist": "Astors",
  "title": "Daddy Didn't Tell Me"
}
\end{verbatim}
Which can be used with AJAX-based backends to fetch the current 
metadata of source \verb+s+

\subsection{Set metadata}
Using \verb+insert_metadata+, you can register a GET handler that
updates the metadata of a given source. For instance:

\begin{verbatim}

# s = some source

# x is of type ((metadata)->unit)*source
# first part is a function used to update
# metadata and second part is the source 
# whose metadata are updated
x = insert_metadata(s)

# Get the function
insert = fst(x)

# Redefine s as the new source
s = snd(x)

# The handler
def set_meta(~method,~protocol,~data,~headers,uri) =
  # Split uri of the form request?foo=bar&...
  # into (request,[("foo","bar"),..])
  x = url.split(uri)

  # Filter out unusual metadata
  meta = metadata.export(snd(x))
  
  # Grab the returned message
  ret =
    if meta != [] then
      insert(meta)
      "OK!"
    else
      "No metadata to add!"
  end

  # Return response
  http_response(
   protocol=protocol,
   code=200,
   headers=[("Content-Type","text/html")],
   data="<html><body><b>#{ret}</b></body></html>"
  )
end

# Register handler on port 700
harbor.http.register(port=7000,"/setmeta",set_meta)
\end{verbatim}
Now, a request of the form \verb+http://server:7000/setmeta?title=foo+
will update the metadata of source \verb+s+ with \verb+[("title","foo")]+. You
can use this handler, for instance, in a custom HTML form.

\section{Limitations}
When using harbor's HTTP server, please be warned that the server is 
\textbf{not} meant to be used under heavy load. Therefore, it should \textbf{not}
be exposed to your users/listeners if you expect many of them. In this
case, you should use it as a backend/middle-end and have some kind of 
caching between harbor and the final user. In particular, the harbor server
is not meant to server big files because it loads their entire content in 
memory before sending them. However, the harbor HTTP server is fully equipped 
to serve any kind of CGI script. 



\section{Interaction with the server}
Liquidsoap starts with one or several scripts as its configuration,
and then streams forever if everything goes well.
Once started, you can still interact with it by means of the \emph{server}.
The server allows you to run commands. Some are general and always available,
some belong to a specific operator. For example the \verb+request.queue()+ instances register commands to enqueue new requests, the outputs register commands
to start or stop the outputting, display the last ten metadata chunks, etc.

The protocol of the server is a simple human-readable one.
Currently it does not have any kind of authentication and permissions.
It is currently available via two media: TCP and Unix sockets.
The TCP socket provides a simple telnet-like interface, available only on
the local host by default.
The Unix socket interface (\emph{cf.} the \verb+server.socket+ setting)
is through some sort of virtual file.
This is more constraining, which allows one to restrict the use of the socket
to some priviledged users.

You can find more details on how to configure the server in the
\href{help.html#settings}{documentation} of the settings key \verb+server+,
in particular \verb+server.telnet+ for the TCP interface and \verb+server.socket+
for the Unix interface.
Liquidsoap also embeds some \href{help.html#server}{documentation}
about the available server commands.

Now, we shall simply enable the Telnet interface to the server,
by setting \verb+set("server.telnet",true)+ or simply passing the \verb+-t+ option on
the command-line.
In a \href{complete_case.html}{complete case analysis} we set up a \verb+request.queue()+
instance to play user requests. It had the identifier \verb+"queue"+.
We are now going to interact via the server to push requests into that queue:

\begin{verbatim}
dbaelde@selassie:~$ telnet localhost 1234
Trying 127.0.0.1...
Connected to localhost.localdomain.
Escape character is '^]'.
queue.push /path/to/some/file.ogg
5
END
request.metadata 5
[...]
END
queue.push http://remote/audio.ogg
6
END
request.trace 6
[...see if the download started/succeeded...]
END
exit
\end{verbatim}
Of course, the server isn't very user-friendly.
But it is easy to write scripts to interact with Liquidsoap in that way,
to implement a website or an IRC interface to your radio.
However, this sort of tool is often bound to a specific usage, so we have
not released any of ours. Feel free to
\href{mailto:savonet-users@lists.sf.net}{ask the community}
about code that you could re-use.

\section{JSON}
\subsection{Exporting values using JSON}
Liquidsoap can export any language value in JSON using
\verb+json_of+.

The format is the following :

\begin{itemize}
\item \verb+() : unit+ -> \verb+null+
\item \verb+true: bool+ -> \verb+true+
\item \verb+"abc" : string+ -> \verb+"abc"+
\item \verb+23 : int+ -> \verb+23+
\item \verb+2.0 : float+ -> \verb+2.0+
\item \verb+[2,3,4] : [int]+ -> \verb+[2,3,4]+
\item \verb+[("f",1),("b",4)] : [(string*int)]+ -> \verb+{"f": 1, "b": 4}+
\item \verb+("foo",123) : string*int+ -> \verb+[ "foo", 123 ]+
\item \verb+s : source+ -> \verb+"<source>"+
\item \verb+r : int ref+ -> \verb+{"reference": 4}+
\item \verb+%mp3 : encoder+ ->
\begin{verbatim}
"%mp3(stereo,bitrate=128,samplerate=44100)" }
\end{verbatim}

\item \verb+r : request+ -> \verb+"<request>"+
\item \verb+f : 'a -> 'b+ -> \verb+"<fun>"+

\end{itemize}
The two particular cases are:

\begin{itemize}
\item Products are exported as lists
\item Lists of type \verb+string*'a+ are exported as object of the form
  \verb+{"key": value}+

\end{itemize}
Output format is pretty printed by default. A compact output can
be obtained by using the optional argument: \verb+compact=true+.

\subsection{Importing values using JSON}
If compiled with json-wheel support, Liquidsoap can also
parse JSON data into values. using \verb+of_json+.

The format is a subset of the format of exported values with the notable
difference that only ground types (\verb+int+, \verb+floats+, \verb+string+, ...)
are supported and not variable references, sources, formats,
requests and functions:

\begin{itemize}
\item \verb+null+ -> \verb+() : unit+
\item \verb+true/false+ -> \verb+true/false : bool+
\item \verb+"abc"+ -> \verb+"abc" : string+
\item \verb+23+ -> \verb+23 : int+
\item \verb+2.0+ -> \verb+2.0 : float+
\item \verb+[2,3,4]+ -> \verb+[2,3,4] : int+
\item \verb+{"f": 1, "b": 4}+ -> \verb+[("f",1),("b",4)] : [(string*int)]+
\item \verb+[ "foo", 123 ]+ -> \verb+("foo",123) : string*int+

\end{itemize}
The JSON standards specify that a proper JSON payload can only be an array or an
object. However, simple integers, floats, strings and null values are
also accepted by Liquidsoap.

The function \verb+of_json+ has the following type:

\begin{verbatim}
  (default:'a,string)->'a
\end{verbatim}
The default parameter is very important in order to assure 
type inference of the parsed value. Its value constrains
the parser to only recognize JSON data of the the default value's 
type and is returned in case parsing fails.

Suppose that we want to receive a list of metadata, encoded as an object:

\begin{verbatim}
{ "title": "foo",
 "artist": "bar" }
\end{verbatim}
Then, you would use of\_json with default value \verb+[("error","fail")]+ and do:

\begin{verbatim}
# Parse metadata from json
m = of_json(default= [("error","fail")], json_string)
\end{verbatim}
The type of the default value constrains the parser. For instance, in the above
example, a JSON string \verb+"[1,2,3,4]"+ will not be accepted and the function
will return the values passed as default.

You can use the default value in two different ways:
\begin{itemize}
\item To detect that the received json string was invalid/could not be parsed to
  the expected type. In the example above, if \verb+of_json+ return a metadata
  value of \verb+[("error","fail")]+ (the default) then you can detect in your
  code that parsing has failed.
\item As a default value for the rest of the script, if you do not want to care
  about parsing errors.. This can be useful for instance for JSON-RPC
  notifications, which should not send any response to the client anyway.
\end{itemize}

\section{Secure command server access}
\subsection{The problem}
The command server provided by liquidsoap is very convenient for 
manipulating a running instance of liquidsoap. However, no authentication
mechanism are provided. 

The telnet server has no authentication and listens
by default on the localhost (\verb+127.0.0.1+) network interface,
which means that it is accessible to any logged user on the machine.

Many users have expressed interest into setting up a secured access
to the command server, using for instance user and password information.
While we understand and share this need, we do not believe this 
is a task that lies into liquidsoap's scope.

An authentication mechanism is not something that should be implemented 
naively. Being SSH, HTTP login or any other mechanism, all these methods
have been, at some point, exposed to security issues. Thus, implementing
our own secure access would require a constant care about possible security
issues.

Rather than doing our own home-made secure acces, we believe that our users should be 
able to define their own secure access to the command server, taking advantage of a 
mainstream authentication mechanism, for instance HTTP or SSH login.

In order to give an example of this approach, we show here how to create
a SSH access to the command server.

\subsection{SSH access to the command server}
In this section, we create a SSH user that, when logging through SSH, 
has only access to the command server. 

First, we enable the unix socket for the command server in liquidsoap:

\begin{verbatim}
set("server.socket",true) 
set("server.socket.path","/path/to/socket")
\end{verbatim}
When started, liquidsoap will create a socket file \verb+/path/to/socket+
that can be used to interact with the command server. For instance,
if your user has read and write rights on the socket file, you can do:

\begin{verbatim}
socat /path/to/socket -
\end{verbatim}
The interface is then exactly the same has for the telnet server.

We define now a new ``shell''. This shell is in fact the invokation of the 
socat command. Thus, we create a \verb+/usr/local/bin/liq_shell+ file with the following
content:

\begin{verbatim}
#!/bin/sh
# We test if the file is a socket, readable and writable.
if [ -S /path/to/socket ] && [ -w /path/to/socket ] && \
   [ -r /path/to/socket ]; then
  socat /path/to/socket -
else
# If not, we exit..
  exit 1
fi
\end{verbatim}
We set this file as executable, and we add it in the list of shells in \verb+/etc/shells+.

Now, we create a user with the \verb+liq_shell+ as its shell:

\begin{verbatim}
adduser --shell /usr/local/bin/liq_shell liq-user
\end{verbatim}
You also need to make sure that \verb+liq-user+ has read and write rights
on the socket file.

Finally, when logging through ssh with \verb+liq-user+, we get:

\begin{verbatim}
11:27 toots@leonard % ssh liq-user@localhost
liq-user@localhost's password: 
Linux leonard 2.6.32-4-amd64 #1 SMP Mon Apr 5 21:14:10 UTC 2010 x86_64

The programs included with the Debian GNU/Linux system are free software;
the exact distribution terms for each program are described in the
individual files in /usr/share/doc/*/copyright.

Debian GNU/Linux comes with ABSOLUTELY NO WARRANTY, to the extent
permitted by applicable law.
Last login: Tue Oct  5 11:26:52 2010 from localhost
help
Available commands:
(...)
| exit
| help [<command>]
| list
| quit
| request.alive
| request.all
| request.metadata <rid>
| request.on_air
| request.resolving
| request.trace <rid>
| uptime
| var.get <variable>
| var.list
| var.set <variable> = <value>
| version

Type "help <command>" for more information.
END
exit
Bye!
END
Connection to localhost closed.
\end{verbatim}
This is an example of how you can use an existing secure access to 
secure the access to liquidsoap's command server. This way, you make sure
that you are using a mainstream secure application, here SSH.

This example may be adapted similarly to use an online HTTP login 
mechanism, which is probably the most comment type of mechanism
intented for the command line server.


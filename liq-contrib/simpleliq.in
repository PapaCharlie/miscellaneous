#!@liquidsoap@

# No loggin for now
set("log.stdout",false)
set("log.file",false)

# Audio streamer implemented in liquidsoap
version = "@VERSION@"

# Detect available outputs
outputs = [
%ifdef output.icecast
"shoutcast",
"icecast.mp3",
"icecast.vorbis",
%endif
"dummy"
]
outs = string.concat(separator=", ", outputs)

# Detect available inputs
inputs = [
%ifdef input.alsa
"alsa",
%endif
%ifdef input.jack
"jack",
%endif
"playlist",
"http",
"directory",
"file"
]
ins = string.concat(separator=", ", inputs)

transitions = [
"crossfade",
"smart_crossfade"
]
trs = string.concat(separator=", ", transitions)

failed = ref false
def shutdown() = 
  failed := true
  shutdown()
end

usage = "Usage: simpleliq [ -h ] [ -i input ] [ -o output ] [ -e effect] [ -t transition] [ -only-script ] [ -local ]
* -h, --help: display this message
* Input can be: #{ins}
* Output can be: #{outs}
* Effect can be: none, compress, normalize, nrj (normalize+compress)
* Transition can be: #{trs}
* -only-script only outputs the corresponding script
* -local plays the stream on the local soundcard

Optional settings:

Settings for alsa input (if enabled):
* -alsa-device: set alsa input device (default: default)

Settings for jack input (if enabled):
* -jack-device: set jack input port name (default: simpleliq)

Settings for playlist, directory and file input:
* -uri: set default media source uri (default: false://)

Settings for Icecast output (if enabled):
* -host: set host (default: localhost)
* -port: set source destination port (default: 8080)
* -password: set source password (default: hackme)
* -genre: set source genre (default: savonet)
* -url: set source URL (default: http://savonet.sf.net)
* -description: set source description (default: Powered by liquidsoap)
* -restart: try to restart the output after a failure (default: false)
* -mount: set mountpoint (default: none)"


# Initial message
print("Starting simpleliq version #{version}")

# Get parameters
input = getopt(default="blank","-i")
output = getopt(default="dummy","-o")
effect = getopt(default="none","-e")
transition = getopt(default="none","-t")

# Get help option
help =
 if getopt("-h") == "1" or getopt("--help") == "1" then
  true
 else
  false
 end

# Initial script:
script = ref ""
script.add = fun (x) -> script := !script ^ x
script.add(
"#!/usr/bin/env liquidsoap
# Script generated by simpleliq

# Enable logging to stdout, disable logging to file
set('log.stdout',true)
set('log.file',false)

")

s = blank(id="none")
%ifdef input.alsa
s = 
  if input == "alsa" then
    device = getopt(default="default","-alsa-device")
    script.add(
"# Input alsa
s = input.alsa(device='#{device}')

")
    input.alsa(id="alsa",device=device)
  else
    s
  end
%endif
%ifdef input.jack
s = 
  if input == "jack" then
    device = getopt(default="simpleliq","-jack-device")
    script.add(
"#Input jack
s = input.jack(id='#{device}')

")
    input.jack(id=device)
  else
    s
  end
%endif
s = 
  if input != "input.jack" and input != "input.alsa" then
    uri = getopt(default="false://","-uri")
    if uri == "false://" then
      shutdown ()
    end
    if input == "playlist" then
      script.add(
"# Playlist input
s = playlist('#{uri}')

")
      playlist(id="playlist",uri)
    elsif input == "directory" then
      script.add(
"# Directory input
s = playlist('#{uri}')

")
      playlist(id="directory",uri)
    elsif input == "http" then
      script.add(
"# Http input
s = input.http('#{uri}')

")
      input.http(id="http",uri)
    elsif input == "file" then
      script.add(
"# File input
s = single('#{uri}')

")
      single(id="file",uri)
    else
      s
    end
  else
    s
  end

if source.id(s) == "dummy" then
  print("No valid input..")
  shutdown ()
end

# Getting effet
e = 
  if effect == "normalize" then
script.add(
"# Adding normalization
s = normalize(s)

")
    (effect,normalize)
  elsif effect == "compress" then
script.add(
"# Adding compression
s = compress(s)

")
    (effect,compress)
  elsif effect == "nrj" then
script.add(
"# Adding normalization and compression
s = nrj(s)

")
    (effect,nrj)
  else
    def id(s) = s end
    ("none",id)
  end

# Getting transition
t = 
  if transition == "crossfade" then
    start_next = float_of_string(getopt(default="7.","-start-next"))
    fade_in = float_of_string(getopt(default="5.","-fade-in"))
    fade_out = float_of_string(getopt(default="5.","-fade-out"))
    script.add(
"# Adding crossfade transitions
s = crossfade(start_next=#{start_next},fade_in=#{fade_in},
              fade_out=#{fade_out},s)

")
    (transition,crossfade(start_next=start_next,fade_in=fade_in,
                          fade_out=fade_out))
  elsif transition == "smart_crossfade" then
    script.add(
"# Adding smart_crossfade transitions
s = smart_crossfade(s)

")
    (transition,smart_crossfade)
  else
    def id(s) = s end
    ("none",id)
  end

# Getting output
script.add(
"# Making source infaillible
s = mksafe(s)

")
o = ("dummy",fun (s) -> output.dummy(id="dummy",s))
%ifdef output.icecast
host = getopt(default="localhost","-host")
port = int_of_string(getopt(default="8000","-port"))
password = getopt(default="hackme","-password")
genre = getopt(default="savonet","-genre")
url = getopt(default="http://savonet.sf.net/","-url")
description = getopt(default="Powered by liquidsoap","-description")
restart = bool_of_string(getopt(default="false","-restart"))
mount = getopt(default="none","-mount")
if mount == "none" and (output == "icecast.mp3" or output == "icecast.vorbis") then
  print("No mount point given for icecast output !")
  shutdown ()
end
o = 
  if output == "shoutcast" then
    script.add(
"# Shoutcast output
output.shoutcast(%mp3, host='#{host}', password='#{password}', 
                 genre='#{genre}', url='#{url}', port=#{port},
                 restart=#{restart}, s)

")
    (output,
    fun (s) -> output.shoutcast(%mp3,id="shoutcast",host=host,password=password,
                                port=port,genre=genre,url=url,restart=restart,s))
  elsif output == "icecast.mp3" then
    script.add(
"# MP3 icecast output
o = output.icecast(%mp3, host='#{host}',
                   password='#{password}', genre='#{genre}',
                   url='#{url}',description='#{description}',
                   restart=#{restart}, mount='#{mount}', port=#{port},
                   s)

")
    (output,
    fun (s) -> output.icecast(%mp3,id="icecast-mp3",
                       host=host,password=password,port=port,
                       genre=genre,url=url,description=description,
                       restart=restart,mount=mount,s))
  else
    o
  end
o = 
  if output == "icecast.vorbis" then
    script.add(
"# Vorbis icecast output
o = output.icecast(%vorbis,host='#{host}', password='#{password}',
                   genre='#{genre}', url='#{url}',
                   description='#{description}', port=#{port},
                   restart=#{restart}, mount='#{mount}',
                   s)

")
   (output,
    fun (s) -> output.icecast(%vorbis,id="icecast-vorbis",
                          host=host,password=password,port=port,
                          genre=genre,url=url,description=description,
                          restart=restart,mount=mount,s))
  else
    o
  end
%endif
o = 
  if getopt("-local") == "1" then
    # Special case for local: added on top..
    print("Enabling local output..")
    script.add(
"# Adding local output
out(s)

")
    def f(s) = 
      ignore(out(s))
      f = snd(o)
      f(s)
    end
    (fst(o),f)
  else
    o
  end

# Add dummy output to script
if fst(o) == "dummy" then
  script.add(
"# Dummy output
output.dummy(s)

")
end

script.add("# That's all folks !")

# Print config
if not help then
  if not !failed then
    print("Streaming config:")
    print("Input:       #{source.id(s)}")
    print("Output:      #{fst(o)}")
    print("Effect:      #{fst(e)}")
    print("Transitions: #{fst(t)}")
    print("Generated script:")
    print("8<--------------------------------->8")
    print(!script)
    print("8<--------------------------------->8")
    if getopt("-only-script") != "1" then
      print("Starting streaming...")
      set("log.stdout",true)
      e = snd(e)
      t = snd(t)
      o = snd(o)
      o(mksafe(t(e(s))))
    else
      shutdown ()
      output.dummy(blank())
    end
  else
    print("Something went wrong...")
    print("Start with --help to get some help.")
    shutdown ()
    output.dummy(blank ())
  end
else
  print(usage)
  shutdown()
  output.dummy(blank())
end
